/*
 * SPDX-License-Identifier: Apache-2.0
 * Copyright Contributors to the ODPi Egeria project.
 */

plugins {
    // Include plug-in for starting a new process
    id 'com.github.psxpaul.execfork'
    id 'java'
}

apply plugin: "com.github.psxpaul.execfork"
apply plugin: 'groovy'

dependencies {
    implementation project(':open-metadata-implementation:access-services:data-manager:data-manager-api')
    implementation project(':open-metadata-implementation:access-services:data-manager:data-manager-client')
    implementation project(':open-metadata-implementation:frameworks:audit-log-framework')
    implementation project(':open-metadata-implementation:frameworks:open-connector-framework')
    implementation project(':open-metadata-implementation:repository-services:repository-services-apis')
    implementation project(':open-metadata-implementation:admin-services:admin-services-api')
    implementation project(':open-metadata-implementation:common-services:ffdc-services')
    implementation project(':open-metadata-implementation:framework-services:ocf-metadata-management:ocf-metadata-client')
    implementation project(':open-metadata-test:open-metadata-fvt:fvt-utilities')
    compileOnly 'com.fasterxml.jackson.core:jackson-annotations'
    testImplementation project(':open-metadata-distribution:open-metadata-assemblies')
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine'
    testImplementation localGroovy()
    compileOnly 'com.fasterxml.jackson.core:jackson-annotations'
    testImplementation("org.junit.jupiter:junit-jupiter-params")
    implementation project(':open-metadata-implementation:adapters:authentication-plugins:http-helper')
    testImplementation 'org.apache.commons:commons-lang3'


}

description = 'Data Manager OMAS FVT'

java {
    withJavadocJar()
}


// Local properties to use
ext
        {
            distdir="../../../../../../open-metadata-distribution/open-metadata-assemblies/build/unpacked/egeria-${project.version}-distribution.tar.gz"
        }


// Build the groovy test script
sourceSets.test.java.srcDirs = []
sourceSets.test.groovy.srcDirs = ["src/test/java", "src/test/script"]

// Initially we launch as an app -- though there may be a better way of running this in the existing jvm
task startServer(dependsOn: ['cleanData',':open-metadata-distribution:open-metadata-assemblies:unzip','prepareDirs'], type: com.github.psxpaul.task.ExecFork) {
    // Start some external service
    executable = System.properties.'java.home' + '/bin/java'
    workingDir = layout.buildDirectory.dir('run')

    args = [ "-Dserver.port=10446",
             "-Dloader.path=${distdir}/server/lib",
             "-Dspring.config.location=../../../../../../test.application.properties",
             "-jar",
             "${distdir}/server/server-chassis-spring-${project.version}.jar" ]
    // Setting the output directories to be the same for ALL FVTs acts as a synchronization mechanism
    // - alternatively we need to ensure the server port is unique per test
    standardOutput = layout.buildDirectory.file("chassis.log")
    errorOutput = layout.buildDirectory.file("chassis-error.log")
    stopAfter = test
    waitForPort = 10446
    timeout = 180

    //waitForOutput = 'has started'
}

// Configuration of the platform
task configServer(dependsOn: ['startServer','classes'], type: JavaExec) {
    mainClass = 'configureStartServer'
    classpath = sourceSets.test.runtimeClasspath
    //TODO: These parms aren't currently correctly passed. We rely on defaults in the script
    systemProperties = [
            'name': 'Egeria chassis',
            'baseURL': 'https://localhost:10446',
            'servermem': 'serverinmem',
            'delay': '2',
            'user': 'testUser'
    ]
}

task prepareDirs {
    mustRunAfter clean
    doLast {
        mkdir layout.buildDirectory.dir('run')
    }
}

task cleanData(type: Delete) {
    delete files("${buildDir}/run/data")
}

// The actual tests (Integration only)
test {
    description 'Run Integration Tests'
    group = 'verification'
    outputs.upToDateWhen {return false}
    systemProperties = [
            'fvt.url': 'https://localhost:10446'
    ]
    useJUnitPlatform()
    testLogging.showStandardStreams = true
    dependsOn configServer

}

// Resolves logging conflict with Jena
loggingCapabilities {
    enforceLogback()// Configuration goes here
}